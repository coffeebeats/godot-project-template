##
## project/ui/tracker/world_tracker_2d.gd
##
## WorldTracker2D drives its parent `Control` to follow a `Node2D` living inside a
## `ProjectMap2D`'s `SubViewport` (e.g. health bars, damage numbers, name plates,
## tooltips, off-screen indicators). See `WorldTracker` for the tracking behavior.
##
## Expected scene layout:
##
##   ProjectMap2D                            (referenced via the `map` export)
##   ├── SubViewportContainer
##   │   └── SubViewport
##   │       └── ... game world ...
##   │           └── Node2D                  (target)
##   └── UI                                  (later subtree than SubViewport)
##       └── HealthBar / Tooltip / etc.      (Control; tracker's parent)
##           └── WorldTracker2D
##

class_name WorldTracker2D
extends WorldTracker

# -- CONFIGURATION ------------------------------------------------------------------- #

## map is the `ProjectMap2D` whose `SubViewport` hosts `target`.
@export var map: ProjectMap2D = null

## target is the `Node2D` the host follows; it must live inside `map.sub_viewport`.
## Reassigning it at runtime re-arms a stopped tracker.
@export var target: Node2D = null:
	set(value):
		target = value
		# Re-arm processing; `_process` disables itself once `target`/`map` go invalid.
		if is_instance_valid(value):
			set_process(true)

# -- PRIVATE METHODS (OVERRIDES) ----------------------------------------------------- #


func _get_map() -> ProjectMap:
	return map


func _get_target() -> Node:
	return target


func _project_target() -> Vector2:
	return map.world_to_screen(target.global_position)
